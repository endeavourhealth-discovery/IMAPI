plugins {
  // Support convention plugins written in Groovy. Convention plugins are build scripts in 'src/main' that automatically become available as plugins in the main build.
  id 'groovy-gradle-plugin'
  id "org.sonarqube" version "4.4.1.3373"
  id 'java'
}

repositories {
  // Use the plugin portal to apply community plugins in convention plugins.
  gradlePluginPortal()
}

apply from: "dependencies.gradle"
sourceCompatibility = '21'

println "Sonar = [$SONAR_LOGIN]"

sonar {
  properties {
    property 'sonar.gradle.skipCompile', true
    property 'sonar.projectKey', 'endeavourhealth-discovery_IMAPI'
    property 'sonar.organization', 'endeavourhealth-discovery'
    property 'sonar.token', SONAR_LOGIN
    property 'sonar.host.url', 'https://sonarcloud.io'
    property 'sonar.junit.reportPaths', 'build/test-results/test/binary'
    property 'sonar.exclusions', '**/imapi/parser/**, **/imapi/transforms/**/eqd/**'
    property 'sonar.coverage.exclusions', '**/imapi/config/**, **/imapi/controllers/**, **/imapi/dataaccess/**, **/imapi/errorhandling/**, **/imapi/filer/**, **/imapi/vocabulary/**, **/imapi/transforms/eqd/**'
  }
}

println "Build environment = [$ENV]"
if (ENV == 'prod') {
  build.finalizedBy 'sonar'
  build.finalizedBy 'publish'
}

allprojects {
  apply plugin: 'java-library'
  apply plugin: 'maven-publish'
  apply plugin: 'jacoco'

  group = 'org.endeavourhealth.imapi'
  version = '1.0-SNAPSHOT'

  sourceCompatibility = JavaVersion.VERSION_21
  targetCompatibility = JavaVersion.VERSION_21

  tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
  }

  jacocoTestReport {
    reports {
      xml.required.set(true)
    }
  }

  test.finalizedBy jacocoTestReport

  repositories {
    mavenLocal()
    mavenCentral()
    maven {
      url = uri('https://artifactory.endhealth.co.uk/repository/maven-releases')
    }
    maven {
      url = uri('https://artifactory.endhealth.co.uk/repository/maven-snapshots')
    }
  }
}

task myJavadocs(type: Javadoc) {
  source = sourceSets.main.allJava
}

